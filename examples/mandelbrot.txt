{! Mandelbrot renderer
 ! Generates a mandelbrot fractal rendered in ASCII uppercase letters
 ! The height and width variables control the size of the fractal
 ! The iterations variable can limit the depth to which the fractal is rendered
 !}

{#>oneline}

{set:height:50;}
{set:width:200;}
{set:iterations:30;}
{set:chars: ABCDEFGHIJKLMNOPQRSTUVWXYZ ;}
{func:{min:x:y;}:
    {if:{le:{x}:{y};}:
        {x}
    :
        {y}
    ;}
;}
{set:char_max:{min:{sub:{chars.length}:1;}:{iterations};};}
{set:transx:{fdiv:4:{width};};}
{set:transy:{fdiv:2:{height};};}
{func:{m:xcoord:ycoord;}:
    {set:x0:{sub:{mul:{xcoord}:{transx};}:2.5;};}
    {set:y0:{sub:{mul:{ycoord}:{transy};}:1;};}
    {set:x2:0;}
    {set:y2:0;}
    {set:w:0;}
    {set:i:0;}
    {while:{and:{le:{add:{x2}:{y2};}:4;}:{le:{i}:{char_max};};}:
        {set:x:{add:{sub:{x2}:{y2};}:{x0};};}
        {set:y:{sub:{add:{w}:{y0};}:{add:{x2}:{y2};};};}
        {set:x2:{mul:{x}:{x};};}
        {set:y2:{mul:{y}:{y};};}
        {set:wt:{add:{x}:{y};};}
        {set:w:{mul:{wt}:{wt};};}
        {set:i:{add:{i}:1;};}
    ;}
    {chars[{min:{i}:{char_max};}]}
;}

{for:ycoord:0:{height}:
    {for:xcoord:0:{width}:
        {m:{xcoord}:{ycoord};}
    ;}\n
;}